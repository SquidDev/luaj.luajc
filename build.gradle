plugins {
	id "com.jfrog.bintray" version "1.4"
}
apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'org.squiddev'
version = '1.0.3'

sourceCompatibility = 1.6

repositories {
	mavenCentral()
}

dependencies {
	compile group: 'org.ow2.asm', name: 'asm-all', version: '5.+'
	compile group: 'org.luaj', name: 'luaj-jse', version: '2.+'

	testCompile group: 'junit', name: 'junit', version: '4.+'
}

task sourcesJar(type: Jar, dependsOn:classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['mavenJava']
	pkg {
		repo = 'maven'
		name = 'luaj.luajc'
		licenses = ['MIT']
		vcsUrl = 'https://github.com/SquidDev/luaj.luajc'

		version {
			name = project.version
			desc = 'LuaJ\'s LuaJC ported to the ASM framework'
			released = new Date()
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar { classifier "sources" }
			artifact javadocJar { classifier "javadoc" }
		}
	}
}

test {
	testLogging {
		events "passed", "skipped", "failed", "standardOut", "standardError"
	}
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
	}
}

task executeProfile(type:JavaExec) {
	main = "org.squiddev.luaj.luajc.PerformanceRunner"
	args = ["--prompt", "--luac"]
	classpath = sourceSets.test.runtimeClasspath
	standardInput = System.in
}
