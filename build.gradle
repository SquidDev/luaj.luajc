apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.7

group = 'org.squiddev'
version = '1.0'

configurations {
	deployerJars
}

repositories {
	mavenCentral()

	maven {
		name = "squiddev"
		url = "http://maven.bonzodandd.co.uk"
	}
}

dependencies {
	compile group: 'org.ow2.asm', name: 'asm-all', version: '5.+'
	compile group: 'org.luaj', name: 'luaj-jse', version: '2.+'

	testCompile group: 'junit', name: 'junit', version: '4.+'

	deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

task sourcesJar(type: Jar, dependsOn:classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives jar

	archives sourcesJar
	archives javadocJar
}

uploadArchives {
	repositories {
		if (!project.hasProperty("ftpHost")) {
			mavenLocal()
		} else {
			mavenDeployer {
				configuration = configurations.deployerJars
				repository(url: ftpHost) {
					authentication(userName: ftpUser, password: ftpPass)
				}

				pom.project {
					name 'luaj.luajc'
					packaging 'jar'
					description 'LuaJ\'s LuaJC ported to the ASM framework'
					url 'https://github.com/SquidDev/luaj.luajc'

					scm {
						url 'scm:git@github.com:SquidDev/luaj.luajc.git'
						connection 'scm:git@github.com:SquidDev/luaj.luajc.git'
						developerConnection 'scm:git@github.com:SquidDev/luaj.luajc.git'
					}

					licenses {
						license {
							name 'MIT License'
							url 'http://opensource.org/licenses/mit-license.php'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'SquidDev'
							name 'SquidDev'
						}
					}
				}
			}
		}
	}
}
